plugins {
    id 'io.freefair.lombok' version '5.3.0'
    id 'org.greenrobot.greendao'
    id 'com.android.application'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'kotlin-android'
    id 'net.researchgate.release' version '2.8.1'
}

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "de.yoadey.choreomusic"
        minSdkVersion 26
        targetSdkVersion 32
        versionCode 12
        versionName "0.12"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        //noinspection DataBindingWithoutKapt
        dataBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                // Only use arm types in the release, as the x86 doesn't have x64 support in waveform
                // and nearly no x86 devices exist anyway
                abiFilters "arm64-v8a", "armeabi-v7a"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true;
    }
    namespace 'de.yoadey.choreomusic'
}

release {
    pushReleaseVersionBranch = 'master'

    git {
        requireBranch = 'develop'
    }
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java', 'build/generated/source/greendao']
    }
}

greendao {
    schemaVersion 4
}

dependencies {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    //noinspection AnnotationProcessorOnCompilePath
    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    // Waveform library
    implementation 'com.github.massoudss:waveformSeekBar:5.0.1'
    implementation 'com.github.lincollincol:Amplituda:2.1.0'

    // Media libraries
    implementation 'com.google.android.exoplayer:exoplayer:2.18.1'
    implementation 'com.google.android.exoplayer:extension-mediasession:2.18.1'
    // Save metadata in MP3 file
    implementation 'com.mpatric:mp3agic:0.9.1'

    // Special activities
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'io.github.medyo:android-about-page:2.0.0'
    implementation 'com.github.AppIntro:AppIntro:6.1.0'
    implementation 'com.github.Dhaval2404:ColorPicker:2.3'

    // DB layer
    implementation 'org.greenrobot:greendao:3.3.0'

    // Android dependencies
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.annotation:annotation:1.4.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.6.21'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.1'

    // Licenses management
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    // Unit testing dependencies
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-inline:4.6.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.6.1'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}